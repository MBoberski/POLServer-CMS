use uo;
use os;
use sql;

include ":webApi:accounts";
include ":webApi:settings";

program GlobalControl()
	var acct, chars, settings, index;

	settings := GetSettingsCfgElem( "Settings" );

	while( 1 )
		var db := mysql_connect(settings.DBHost,settings.DBUser,password := settings.DBPass);

		if( db )
			mysql_select_db(db,settings.DBName);
			
			mysql_query(db,"TRUNCATE TABLE "+settings.CharTable);
			SleepMS(5);
			mysql_query(db,"TRUNCATE TABLE "+settings.CharLayers);
			SleepMS(5);

			foreach acct_name in ( ListAccounts() )
				acct := FindAccount( acct_name );
				
				if ( GetNumCharacters( acct ) > 0 )
					chars := GetCharacters( acct );
					foreach charRef in ( chars )
						if ( charRef )
							var race := "";
							
							if (charRef.trueobjtype == 0x190 || charRef.trueobjtype == 0x191)
								race := "Human";
							elseif (charRef.trueobjtype == 0x25D || charRef.trueobjtype == 0x25E)
								race := "Elf";
							elseif (charRef.trueobjtype == 0x29A || charRef.trueobjtype == 0x29B)
								race := "Gargoyle";
							else 
								race := "Other";
							endif
						
							mysql_query(db,"INSERT INTO "+settings.CharTable+" (char_id, char_name, char_title, char_race, char_body, char_female, char_bodyhue, char_public, char_cmdlevel ) VALUES (?, ?, ?, ?, ?, ?, ?, ?, ?)",array{charRef.serial, charRef.name, charRef.title_prefix, race, charRef.trueobjtype, charRef.gender, charRef.truecolor, 0, charRef.cmdlevel});
							
							SleepMS(5);
							
							var x, i;
							for (i := 1; i <= 24; i := i + 1)
								x := GetEquipmentByLayer(charRef, i);
								
								if ( (i == 0x0b) || (i == 0x10) || (i == 0x15) )
									mysql_query(db,"INSERT INTO "+settings.CharLayers+" (char_id, layer_id, item_id, item_hue) VALUES (?, ?, ?, ?)",array{charRef.serial, 0, x.objtype, x.color});
								else
									mysql_query(db,"INSERT INTO "+settings.CharLayers+" (char_id, layer_id, item_id, item_hue) VALUES (?, ?, ?, ?)",array{charRef.serial, i, x.objtype, x.color});
								endif
								
								SleepMS(5);
							endfor
							
						endif

						SleepMS(5);
					endforeach
				endif
				
				SleepMS(5);
			endforeach
			
			mysql_close(db);
		else
			SysLog ("DBError: "+db.errortext);
		endif
		
		Sleep(CInt(settings.CharExportDelay) * 60);
	endwhile
endprogram
